* HP Memristor SPICE Model Using Joglekar Window

* Connections:
* TE:  Top electrode
* BE:  Bottom electrode
* XSV: External connection to plot state variable
*      that is not used otherwise

.SUBCKT MEM_JOGLEKAR TE BE XSV 

* Ron: Minimum device resistance
* Roff: Maximum device resistance
* D:   Width of the thin film
* uv:  Dopant mobility
* p:   Parameter for window function
* x0:   State variable initial value

.params Ron=100 Roff=10K x0=.56 D=12N uv=50F p=7

* Joglekar Window Function
.func f(V1) = 1-pow((2*V1-1),(2*p))

* Memristor I-V Relationship
.func IVRel(V1,V2) = V1/(Ron*V2 + Roff*(1-V2))

* Circuit to determine state variable
Gx 0 XSV value={ I(Gmem)*Ron*uv*f(V(XSV,0))/pow(D,2) }
Cx XSV 0 {1}
.ic V(XSV) = x0

* Current source representing memristor
Gmem TE BE value={IVRel(V(TE,BE),V(XSV,0))}

.ENDS MEM_JOGLEKAR
